buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.10'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id "io.sentry.jvm.gradle" version "3.11.0"
    id 'checkstyle'
}

group = 'com.universe'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

tasks.withType(Checkstyle).configureEach {
    it.source 'src/main/java'
    it.exclude 'build/classes/java/main'

    reports {
        xml.required = true
    }
}

checkstyle {
    configFile = file("checkstyle/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "checkstyle/naver-checkstyle-suppressions.xml"]
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.8"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // sentry dependency
    implementation 'io.sentry:sentry-spring-boot-starter:6.23.0'
    implementation 'io.sentry:sentry-logback:6.23.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    def queryDslVersion = "5.0.0"
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jpa"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"

    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    implementation "org.springdoc:springdoc-openapi-ui:1.6.11"

    def mapstructVersion = "1.5.5.Final"
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //JWT
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register("copyConfig", Copy) {
    from('./uni-config')
    include('src/**/*.yml')
    into('.')
    eachFile { file ->
        println "> copy file: ${file.sourcePath}"
    }
}

copy {
    from('./uni-config')
    include('src/**/sentry.yml')
    into('.')
    eachFile { file ->
        println "> copy file: ${file.sourcePath}"
    }
}



def sentryProperties = new Properties()
file("src/main/resources/sentry.yml").withInputStream { sentryProperties.load(it) }


sentry {
    debug = true
    includeSourceContext = true

    org = "uni-universe"
    projectName = "sparkle-dev-be"
    authToken = sentryProperties.getProperty("sentry-auth-token")
}

def querydslDir = "$buildDir/generated/sources/annotationProcessor"

sourceSets {
    delete(file(querydslDir))
    main.java.srcDir querydslDir
}